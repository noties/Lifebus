buildscript {

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.2'
    }
}

allprojects {

    repositories {
        google()
        jcenter()
    }

    group = GROUP
    version = VERSION_NAME

    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task wrapper(type: Wrapper) {
    gradleVersion '4.10.2'
    distributionType 'all'
}

def releaseToMaven() {
    return project.hasProperty('release')
}

ext {

    config = [
            'min-sdk'    : 16,
            'target-sdk' : 27,
            'build-tools': '28.0.3'
    ]

    final def supportVersion = '27.1.0'
    final def lifecycleVersion = '1.1.1'

    deps = [
            'support-annotations': "com.android.support:support-annotations:$supportVersion",
            'support-fragments'  : "com.android.support:support-fragment:$supportVersion",
            'support-app-compat' : "com.android.support:appcompat-v7:$supportVersion",
            'lifecycle-ext'      : "android.arch.lifecycle:extensions:$lifecycleVersion",
            'lifecycle-java8'    : "android.arch.lifecycle:common-java8:$lifecycleVersion",
            'debug'              : 'ru.noties:debug:4.0.0@jar'
    ]

    testDeps = [
            'junit'              : 'junit:junit:4.12',
            'mockito'            : 'org.mockito:mockito-core:2.16.0',
            'robolectric'        : 'org.robolectric:robolectric:3.8'
    ]

    releaseToMaven = this.&releaseToMaven
}
